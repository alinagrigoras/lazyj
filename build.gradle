// Adding provided and optional dependencies configuration
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'provided-base'
apply plugin: 'maven'
apply plugin: 'signing'

description = "LazyJ is a rapid development framework for Java WEB applications"
version = "20150519"

// Task: Creating the JAliEn jar
jar {
	manifest {
		attributes 'Implementation-Title': 'lazyj',
		'Implementation-Version': version
	}
}

// Task: Creating the JavaDoc
task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

// Task: Creating the sources jar
task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

// Dependencies or publishing repositories
repositories {
	mavenCentral()

	flatDir {
		dirs 'src/main/resources'
	}
}

dependencies {
	provided 'javax.servlet:javax.servlet-api:3.1.0'

	compile 'javax.servlet:javax.servlet-api:3.1.0'
	compile 'javax.mail:javax.mail-api:1.5.3'
	compile 'ymsg:ymsg-network:0.6.1'
	compile 'com.servlets:cos:09May2002'
}

// Adding the artifact to archives to be uploaded
artifacts {
	archives javadocJar, sourcesJar
}

// Signing the artifacts
signing {
	sign configurations.archives
}

group = "com.github.costing"
archivesBaseName = "lazyj"

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'LazyJ'
				packaging 'jar'
				description 'A rapid development framework for Java WEB applications'
				url 'https://github.com/costing/lazyj'

				scm {
					connection 'scm:https://github.com/costing/lazyj.git'
					developerConnection 'scm:https://github.com/costing/lazyj.git'
					url 'https://github.com/costing/lazyj'
				}

				licenses {
					license {
						name 'GNU LESSER GENERAL PUBLIC LICENSE V3'
						url 'http://www.gnu.org/licenses/lgpl.html'
					}
				}

				developers {
					developer {
						id 'costing'
						name 'Costin Grigoras'
						email 'costing@gmail.com'
					}
				}
			}
		}
	}
}

